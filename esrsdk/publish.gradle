apply plugin: 'maven-publish'

def LIB_GROUP_ID = 'com.greymass'
def LIB_ARTIFACT_ID = 'esrsdk'
def LIB_VERSION = '1.0.0'

task sourceJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier "sources"
}

publishing {
	repositories {
		maven {
			name = "GithubPackages"
			url = uri("https://maven.pkg.github.com/ProtonProtocol/eosio-signing-request-java")
			credentials {
				Properties properties = new Properties()
				properties.load(project.rootProject.file('local.properties').newDataInputStream())

				username = properties.getProperty('github.username')
				password = properties.getProperty('github.token')
			}
		}
		maven {
			name = 'CustomMavenRepo'
			url = "file://${buildDir}/repo"
		}
	}
	publications {
		esrsdk(MavenPublication) {
			groupId LIB_GROUP_ID
			artifactId LIB_ARTIFACT_ID
			version LIB_VERSION
			artifact("$buildDir/outputs/aar/esrsdk-release.aar")
			artifact(sourceJar)

			pom.withXml {
				def dependenciesNode = asNode().appendNode('dependencies')

				//Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
				configurations.implementation.allDependencies.each {
					def dependencyNode = dependenciesNode.appendNode('dependency')
					dependencyNode.appendNode('groupId', it.group)
					dependencyNode.appendNode('artifactId', it.name)
					dependencyNode.appendNode('version', it.version)
				}
			}
		}
	}
}